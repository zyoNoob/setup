_tv_smart_autocomplete() {
    emulate -L zsh
    zle -I

    local current_prompt
    current_prompt=$LBUFFER

    local output
    # Get output and replace newlines with spaces
    output=$(tv --inline --autocomplete-prompt "$current_prompt" $* | tr '\n' ' ')

    # Check if the output ends with a space and remove it
    if [[ "${output}" == *" " ]]; then
        # Use % to remove the shortest suffix match (a single trailing space)
        output="${output% }"
    fi

    if [[ -n $output ]]; then
        # suffix (rhs of cursor)
        local rhs=$RBUFFER

        # Case 1: Empty prompt - don't add space, keep output as is
        if [[ -z "${current_prompt}" ]]; then
            # Do nothing to the output or prompt
        
        # Case 2: Prompt contains './' - don't add space, remove './' from output if present
        elif [[ "${current_prompt}" == *"./"* ]]; then
            if [[ "${output}" == "./"* ]]; then
                output="${output#./}"
            fi
            
        # Case 3: Any other case - add space to prompt, remove './' from output if present
        else
            if [[ "${output}" == "./"* ]]; then
                output="${output#./}"
            fi
            
            if [[ "${current_prompt}" != *" " ]]; then
                current_prompt="${current_prompt} "
            fi
        fi
        
        LBUFFER=$current_prompt$output
        CURSOR=${#LBUFFER}
        RBUFFER=$rhs

        zle reset-prompt
        # uncomment this to automatically accept the line 
        # (i.e. run the command without having to press enter twice)
        # zle accept-line
    fi
}

_tv_shell_history() {
    emulate -L zsh
    zle -I

    local current_prompt
    current_prompt=$LBUFFER

    local output

    output=$(tv zsh-history --input "$current_prompt" $*)


    if [[ -n $output ]]; then
        zle reset-prompt
        RBUFFER=""
        LBUFFER=$output

        # uncomment this to automatically accept the line 
        # (i.e. run the command without having to press enter twice)
        # zle accept-line
    fi
}


zle -N tv-smart-autocomplete _tv_smart_autocomplete
zle -N tv-shell-history _tv_shell_history


bindkey '^T' tv-smart-autocomplete
